include "sky_shader_global.dshl"
include "viewVecVS.dshl"
include "clouds_tiled_dist.dshl"
include "clouds_close_layer_outside.dshl"
include "skies_special_vision.dshl"
include "distanceToClouds2.dshl"
include "vr_reprojection.dshl"
include "use_custom_fog_sky.dshl"
include "flexible_scale_rasterization.dshl"
include "cloud_volume_mask.dshl"

float min_ground_offset;
texture clouds_color;
texture clouds_color_close;

texture clouds_target_depth_gbuf;
float4 clouds_target_depth_gbuf_transform = (1, 1, 0, 0);
texture clouds_depth_gbuf;
int4 clouds2_resolution;
int clouds_has_close_sequence = 1;
int clouds_apply_simple_ztest = 1;

texture dynamic_bit_texture;

int clouds_use_blur_apply = 0;
interval clouds_use_blur_apply: no < 1, yes;

shader clouds2_apply, clouds2_apply_has_empty, clouds2_apply_no_empty
{
  blend_src = 1; blend_dst = isa;
  cull_mode=none;
  z_write=false;
  if (dynamic_bit_texture == NULL)
  {
    z_test=true;
  }
  else
  {
    z_test=false;
  }

  if (clouds_depth_gbuf == NULL)
  {
    hlsl {
      #define SIMPLE_APPLY 1
    }
  }
  USE_CLOUDS_DISTANCE(ps)
  USE_CLOUDS_DISTANCE_STUB(ps)
  USE_SPECIAL_VISION()
  INIT_HDR(ps)
  USE_HDR(ps)

  INIT_BOUNDING_VIEW_REPROJECTION(ps)
  USE_BOUNDING_VIEW_REPROJECTION(ps)

  CLOUD_VOLUME_MASK()

  USE_CUSTOM_FOG_SKY(ps)

  VIEW_VEC_OPTIMIZED(ps)
  ENABLE_ASSERT(ps)

  (ps)
  {
    world_view_pos__clouds_start_alt@f4 = (world_view_pos.x, world_view_pos.y, world_view_pos.z, clouds_start_altitude2 * 1000.0);
  }
  hlsl(ps) {
    #define world_view_pos world_view_pos__clouds_start_alt.xyz
    #define clouds_start_alt world_view_pos__clouds_start_alt.w
  }

  hlsl {
    struct VsOutput
    {
      VS_OUT_POSITION(pos)
      float3 viewVect : TEXCOORD0;
      float2 tc : TEXCOORD1;
    };
  }

  USE_POSTFX_VERTEX_POSITIONS()
  USE_AND_INIT_VIEW_VEC_VS()
  (vs) {
    encode_depth@f3 = (zn_zfar.x / (zn_zfar.y - zn_zfar.x), zn_zfar.y*zn_zfar.x / (zn_zfar.y - zn_zfar.x),
      max(40, clouds_start_altitude2 * 1000 - world_view_pos.y), 0);
    clouds_apply_simple_ztest@f1 = (clouds_apply_simple_ztest, 0, 0, 0);
  }

  hlsl(vs) {
    VsOutput apply_clouds_vs(uint vertexId : SV_VertexID)
    {
      VsOutput output;
      float minCloudDist = encode_depth.z;
      float z = minCloudDist*encode_depth.x+encode_depth.y;
      #if SIMPLE_APPLY
      if (clouds_apply_simple_ztest > 0)
        { minCloudDist = 1; z = 0; }
      #endif
      float2 pos = getPostfxVertexPositionById(vertexId);
      output.pos = float4(pos.xy*minCloudDist, z, minCloudDist);
      output.tc = screen_to_texcoords(pos);
      output.viewVect = get_view_vec_by_vertex_id(vertexId);
      return output;
    }
  }

  (ps) {
    fullres_depth_gbuf@smp2d = clouds_target_depth_gbuf;
    fullres_depth_gbuf_transform@f4 = clouds_target_depth_gbuf_transform;
    clouds_depth_gbuf@smp2d = clouds_depth_gbuf;

    clouds_color@smp2d = clouds_color;
    clouds_color_close@smp2d = clouds_color_close;
    clouds2_far_res@f4 = (clouds2_resolution.xy, 1/clouds2_resolution.xy);
    clouds2_far_resI@u2 = (clouds2_resolution.xy);
    clouds2_close_res@f2 = (clouds2_resolution.zw,0,0);
    clouds_has_close_sequence@f1 = (clouds_has_close_sequence);

    dynamic_bit_texture@smp2d = dynamic_bit_texture;
  }
  INIT_ZNZFAR()
  DISTANCE_TO_CLOUDS2(ps)
  CLOSE_LAYER_EARLY_EXIT(ps)
  USE_FSR(ps)

  if (is_gather4_supported == supported || shader != clouds2_apply)
  {
    hlsl {
      #define HAS_GATHER 1
    }
  }

  if (shader == clouds2_apply_no_empty)
  {
    hlsl {
      #define HAS_EMPTY_TILES 0
    }
  } else if (shader == clouds2_apply_has_empty || shader == clouds2_apply)
  {
    hlsl {
      #define HAS_EMPTY_TILES 1
    }
  }

  hlsl(ps) {
    #include "daCloudsTonemap.hlsl"
    #include "cloud_settings.hlsli"
    float4 getZWeights(float4 lowResLinearZ, float hiResLinearZ)
    {
      return rcp( 0.00001 + abs( lowResLinearZ - hiResLinearZ ) );
    }

    uint getDynamic(float2 uv)
    {
      ##if dynamic_bit_texture == NULL
        return 0;
      ##else
        uv = uv * fullres_depth_gbuf_transform.xy + fullres_depth_gbuf_transform.zw;
        half shadowPacked = tex2Dlod(dynamic_bit_texture, float4(uv,0,0)).w;
        return shadowPacked > 0.5;
      ##endif
    }

    #include <tex2d_bicubic.hlsl>
    half4 getBicubicClose( float2 p )
    {
##if clouds_use_fullres == yes
      return tex2Dlod(clouds_color_close, float4(p,0,0));
##else
      return tex2D_bicubic_lod(clouds_color_close, clouds_color_close_samplerstate, p, clouds2_close_res, 0);
##endif
    }

    half4 bilateral_get(float3 viewVec, float2 texcoord, float2 depth_texcoord, float linearDepth, float rawDepth)
    {
      float3 worldPos = viewVec*linearDepth + world_view_pos;
      bool isFullRayBelowClouds = worldPos.y < clouds_start_alt && world_view_pos.y < clouds_start_alt;
      if (isFullRayBelowClouds && rawDepth != 0)
        return half4(0,0,0,0);

##if clouds_use_fullres == yes || clouds_use_blur_apply == yes
      return tex2Dlod(clouds_color, float4(texcoord,0,0));
##else
      #define GET_LOWRES_COORD(ofs, tc)\
        float2 lowResCoords = tc*clouds2_far_res.xy - ofs;\
        int4 lowResCoordsI;\
        lowResCoordsI.xy = int2(lowResCoords);\
        lowResCoordsI.zw = min(lowResCoordsI.xy+int2(1,1), int2(clouds2_far_resI) - 1);

      #if HAS_GATHER
        float4 lowResRawDepth = clouds_depth_gbuf.GatherRed(clouds_depth_gbuf_samplerstate, depth_texcoord).wzxy;
      #else
        GET_LOWRES_COORD(0.5, depth_texcoord)
        float4 lowResRawDepth;
        lowResRawDepth.x = clouds_depth_gbuf[lowResCoordsI.xy].x;
        lowResRawDepth.y = clouds_depth_gbuf[lowResCoordsI.zy].x;
        lowResRawDepth.z = clouds_depth_gbuf[lowResCoordsI.xw].x;
        lowResRawDepth.w = clouds_depth_gbuf[lowResCoordsI.zw].x;
      #endif
      float4 linearLowResDepth = linearize_z4(lowResRawDepth, zn_zfar.zw);
      float4 maxDiff4 = abs(linearLowResDepth-linearDepth);
      float maxDiff = max(max(maxDiff4.x, maxDiff4.y), max(maxDiff4.z, maxDiff4.w));
      float minDiff = min(min(maxDiff4.x, maxDiff4.y), min(maxDiff4.z, maxDiff4.w));
      BRANCH
      if (maxDiff<linearDepth*0.05 || minDiff>linearDepth*0.5)//if difference is within 5%  - just use bilinear
        return tex2Dlod(clouds_color, float4(texcoord,0,0));

      #if HAS_GATHER
        GET_LOWRES_COORD(0.499, texcoord)
      #endif
      float4 linearLowResWeights = getZWeights(linearLowResDepth, linearDepth);
      linearLowResWeights*=linearLowResWeights;
      linearLowResWeights /= dot(linearLowResWeights, 1);

      return clouds_color[lowResCoordsI.xy]*linearLowResWeights.x+
             clouds_color[lowResCoordsI.zy]*linearLowResWeights.y+
             clouds_color[lowResCoordsI.xw]*linearLowResWeights.z+
             clouds_color[lowResCoordsI.zw]*linearLowResWeights.w;
##endif
    }

    half4 blend_result(half4 close_plane, half4 dist_plane, half alpha_mask = 0)
    {
      half4 clouds = half4(TAA_BRIGHTNESS_SCALE*(dist_plane.rgb*(1-close_plane.a) + close_plane.rgb), 1-(1-close_plane.a)*(1-dist_plane.a));
      return lerp(clouds, 0, alpha_mask);
    }

    half4 apply_clouds_ps_main(VsOutput input, float4 screenpos, out float raw_depth)
    {
      float2 linearTc = getLinearTc(screenpos.xy, input.tc);
      float2 depth_texcoord = linearTc;

      #ifdef FSR_DISTORTION
        raw_depth = texelFetchOffset(fullres_depth_gbuf, screenpos.xy, 0, 0).x;
      #else
        raw_depth = tex2Dlod(fullres_depth_gbuf, float4(input.tc.xy * fullres_depth_gbuf_transform.xy + fullres_depth_gbuf_transform.zw,0,0)).x;
      #endif

      // On DX10 we cannot use depth as a target and as a shader resouce at the same time. Do the depth test in the shader
      ##if !hardware.fsh_5_0
        if (screenpos.z < raw_depth)
          return 0.0f;
      ##endif

      float2 texcoord = input.tc;
      bool isDynamic = getDynamic(getLinearTc(input.pos.xy, input.tc));

      ##if use_bounding_vr_reprojection == on
        texcoord = vr_bounding_view_reproject_tc(texcoord, raw_depth);
      ##endif

      if (isDynamic)
      {
        float linearDepth = linearize_z(raw_depth, zn_zfar.zw);
        float mask = get_screen_cloud_volume_mask(texcoord, linearDepth);

        half4 distPlane = tex2Dlod(clouds_color, float4(texcoord,0,0));
        half4 closePlane = getBicubicClose(texcoord);
        return blend_result(closePlane, distPlane, mask);
      }

      #if SIMPLE_APPLY
        half4 distPlane = 0;
        if (HAS_EMPTY_TILES==0 || !tile_is_empty(uint2(texcoord.xy*clouds2_far_res.xy)))
          distPlane = tex2Dlod(clouds_color, float4(texcoord,0,0));
        return half4(TAA_BRIGHTNESS_SCALE*distPlane.rgb, distPlane.a);
      #else
        //we can exit early doing bilteral sample apply, if (for bilinear):
        // +tile is empty (only apply to 'far plane' (of if there is no 'close plane')
        // *all 4 texels of high res depth are zfar. which is the same, as sampling close_depth_tex from required mip and check if it's z-far.
        // *closest possible distance to cloud is still bigger than hires sample depth (happens often on a ground)
        // *all 4 lowres texels of lowres clouds are 0 (no clouds there)

        //todo: check tile and exit immediately if close_layer_should_early_exit(), otherwise just apply close layer.

        float3 view = normalize(input.viewVect);

        #ifndef CHECK_DIST_TO_CLOUDS
          #define CHECK_DIST_TO_CLOUDS 0
        #endif

        #if CHECK_DIST_TO_CLOUDS
          //can happen only when we are above/below clouds layer
          // tht is so rare, that doesn't make sense to optimize
          float distToClouds = 0;
          float dist1; distance_to_clouds(-view, distToClouds, dist1);
          distToClouds *= 1000;
        #endif

        float linearDepth = linearize_z(raw_depth, zn_zfar.zw);
        float linearDist = linearDepth*length(input.viewVect);
        half4 distPlane = 0;
        half4 closePlane = 0;
        BRANCH
        if (!close_layer_should_early_exit())
        {
          #if SIMPLER_CLOSE_CLOUDS_APPLY
          closePlane = tex2Dlod(clouds_color_close, float4(texcoord,0,0));
          #else
          closePlane = getBicubicClose(texcoord);
          #endif
        }
        float closeSequenceEndDist = clouds_has_close_sequence ? closeSequenceStepSize*(closeSequenceSteps-4) : 0;
        #if CHECK_DIST_TO_CLOUDS
        //can happen only when we are above/below clouds layer
        // tht is so rare, that doesn't make sense to optimize
        if (distToClouds > linearDist && raw_depth != 0)
        {
        } else
        #endif
        {
          if (linearDist > closeSequenceEndDist && (HAS_EMPTY_TILES==0 || !tile_is_empty(uint2(texcoord.xy*clouds2_far_res.xy))))
            distPlane = bilateral_get(input.viewVect, texcoord, depth_texcoord, linearDepth, raw_depth);
        }
        return blend_result(closePlane, distPlane);
      #endif
    }

    float4 apply_clouds_ps(VsOutput input HW_USE_SCREEN_POS) : SV_Target
    {
      float4 screenpos = GET_SCREEN_POS(input.pos);
      float rawDepth;
      half4 result = apply_clouds_ps_main(input, screenpos, rawDepth);

      applySpecialVision(result);

      // fog is applied here, in clouds apply instead of sky rendering, as clouds are drawn later, but we don't want +1 pass for fog apply
      // we assume volfog is always in front of clouds (we already assume that for every surface in the depth buffer anyway)
      // otherwise we would apply fog multiple times in a certain range (from encode_depth.z)
      if (rawDepth == 0)
      {
        result.a = 1 - result.a; // transmittance is the inverse of blending alpha
        float2 jitteredVolfogTc = get_volfog_dithered_screen_tc(screenpos.xy, input.tc.xy);
        apply_sky_custom_fog(result, input.tc.xy, jitteredVolfogTc);
        result.a = 1 - result.a;
      }
      result.rgb = pack_hdr(result.rgb);
      return float4(result);
    }
  }

  compile("target_vs", "apply_clouds_vs");
  if (is_gather4_supported == supported || shader != clouds2_apply)
  {
    compile("ps_4_1", "apply_clouds_ps");
  } else
  {
    compile("target_ps", "apply_clouds_ps");
  }
}
float4 clouds2_dispatch_groups;
shader clouds_create_indirect
{
  CLOSE_LAYER_EARLY_EXIT(cs)

  (cs) {
    tiles_threshold@f1 = (clouds_tiled_res.x*clouds_tiled_res.y*0.9, clouds2_dispatch_groups.x,clouds2_dispatch_groups.y,0);//90% of tiles should be non empty so we ignore non-empty tiles completely
    clouds2_dispatch_groups@f4 = clouds2_dispatch_groups;
  }

  hlsl(cs) {
    RWByteAddressBuffer indirect_buffer:register(u0);
    #include <clouds2/cloud_settings.hlsli>

    [numthreads(CLOUDS_APPLY_COUNT, 1, 1)]
    void cs_main(uint flatIdx : SV_GroupIndex)
    {
      uint2 groups = uint2(asuint(clouds2_dispatch_groups.x), asuint(clouds2_dispatch_groups.y));
      bool noEmpty = clouds_non_empty_tile_count_ge(tiles_threshold.x);
      uint targetId = noEmpty ? CLOUDS_NO_EMPTY : CLOUDS_HAS_EMPTY;
      FLATTEN
      if (flatIdx%CLOUDS_APPLY_COUNT_PS == CLOUDS_HAS_CLOSE_LAYER)
      {
        groups = uint2(asuint(clouds2_dispatch_groups.z), asuint(clouds2_dispatch_groups.w));
        targetId = close_layer_should_early_exit() ? 10000 : flatIdx%CLOUDS_APPLY_COUNT_PS;
      }
      uint3 write_first = flatIdx>=CLOUDS_APPLY_COUNT_PS ? uint3(groups, 1) : uint3(3,1,0);
      bool shouldWrite = (flatIdx%CLOUDS_APPLY_COUNT_PS) == targetId;
      storeBuffer(indirect_buffer, (flatIdx * 4 + 0) * 4, shouldWrite ? write_first.x : 0);
      storeBuffer(indirect_buffer, (flatIdx * 4 + 1) * 4, shouldWrite ? write_first.y : 0);
      storeBuffer(indirect_buffer, (flatIdx * 4 + 2) * 4, shouldWrite ? write_first.z : 0);
      storeBuffer(indirect_buffer, (flatIdx * 4 + 3) * 4, 0);
    }
  }
  compile("cs_5_0", "cs_main");
}

macro CLOUD_BLUR(stage)
  (stage){
    clouds_color@smp2d = clouds_color;
    clouds2_far_res@f4 = (clouds2_resolution.xy, 1/clouds2_resolution.xy);
  }

  hlsl(stage) {
    float4 get_simple_blur(float2 texcoord)
    {
      float2 scaledOffsets = clouds2_far_res.zw;
      float2 offsets[8] = {
        -scaledOffsets, // top-left
        float2( 0.0, -scaledOffsets.y), // top-center
        float2( scaledOffsets.x, -scaledOffsets.y), // top-right
        float2(-scaledOffsets.x,  0.0), // mid-left
        float2( scaledOffsets.x,  0.0), // mid-right
        float2(-scaledOffsets.x,  scaledOffsets.y), // bottom-left
        float2( 0.0,  scaledOffsets.y), // bottom-center
        scaledOffsets  // bottom-right
      };

      float offsetsWeights[8]= {
        0.3678, // exp(-1)
        0.6065, // exp(-0.5)
        0.3678,
        0.6065,
        0.6065,
        0.3678,
        0.6065,
        0.3678
      };

      float4 outputColor = float4(0.0, 0.0, 0.0, 0.0);
      UNROLL
      for (int i = 0; i < 8; i++) {
        outputColor += tex2Dlod(clouds_color, float4(texcoord + offsets[i], 0, 0)) * offsetsWeights[i];
      }

      outputColor /= 3.8972; // sum of weights
      return outputColor;
    }
  }
endmacro

shader clouds2_apply_blur_cs
{
  ENABLE_ASSERT(cs)
  CLOUD_BLUR(cs)

  hlsl(cs) {
    #include "cloud_settings.hlsli"
    RWTexture2D<float4> target0: register(u0);

    [numthreads(CLOUD_TRACE_WARP_X, CLOUD_TRACE_WARP_Y, 1)]
    void cs_main(uint2 dtid : SV_DispatchThreadID)
    {
      if (any(dtid >= clouds2_far_res.xy))
        return;
      float2 texcoord = dtid*clouds2_far_res.zw + 0.5*clouds2_far_res.zw;
      texture2DAt(target0, dtid) = get_simple_blur(texcoord);
    }
  }
  compile("cs_5_0", "cs_main");
}

shader clouds2_apply_blur_ps
{
  cull_mode=none;
  z_write=false;
  z_test=false;

  USE_POSTFX_VERTEX_POSITIONS()
  CLOUD_BLUR(ps)

  hlsl {
    struct VsOutput
    {
      VS_OUT_POSITION(pos)
      float2 tc : TEXCOORD0;
    };
  }

  hlsl(vs) {
    VsOutput blur_vs(uint vertexId : SV_VertexID)
    {
      VsOutput output;
      float2 pos = getPostfxVertexPositionById(vertexId);
      output.pos = float4(pos.xy, 0, 1);
      output.tc = screen_to_texcoords(pos);

      return output;
    }
  }

  hlsl(ps) {
    float4 blur_ps(VsOutput IN) : SV_Target
    {
      return get_simple_blur(IN.tc);
    }
  }

  compile("target_vs", "blur_vs");
  compile("target_ps", "blur_ps");
}
