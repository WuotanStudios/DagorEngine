include "mulPointTm_inc.dshl"

float4 hero_matrixX = (0,0,0,1);
float4 hero_matrixY = (0,0,0,1);
float4 hero_matrixZ = (0,0,0,1);

float4 prev_hero_matrixX = (0,0,0,1);
float4 prev_hero_matrixY = (0,0,0,1);
float4 prev_hero_matrixZ = (0,0,0,1);
float4 hero_bbox_reprojectionX = (1,0,0,0);
float4 hero_bbox_reprojectionY = (0,1,0,0);
float4 hero_bbox_reprojectionZ = (0,0,1,0);
int hero_is_cockpit = 0;

macro INIT_HERO_MATRIX_NO_GBUF(code)
  (code) {
    hero_matrixX@f4 = hero_matrixX;
    hero_matrixY@f4 = hero_matrixY;
    hero_matrixZ@f4 = hero_matrixZ;
    prev_hero_matrixX@f4 = prev_hero_matrixX;
    prev_hero_matrixY@f4 = prev_hero_matrixY;
    prev_hero_matrixZ@f4 = prev_hero_matrixZ;
    hero_bbox_reprojectionX@f4 = hero_bbox_reprojectionX;
    hero_bbox_reprojectionY@f4 = hero_bbox_reprojectionY;
    hero_bbox_reprojectionZ@f4 = hero_bbox_reprojectionZ;
    hero_is_cockpit@i1 = (hero_is_cockpit);
  }
endmacro

macro INIT_HERO_MATRIX(code)
  INIT_READ_IS_HERO_COCKPIT(code)
  INIT_HERO_MATRIX_NO_GBUF(code)
endmacro

macro USE_HERO_MATRIX_NO_GBUF(code)
  hlsl(code) {
    bool is_inside_hero_bbox(float3 eye_to_point)
    {
      return all(abs(mulPointTm(eye_to_point, hero_matrixX, hero_matrixY, hero_matrixZ)) < 1);
    }

    float3 reproject_point_in_hero_bbox(float3 eye_to_point)
    {
      return mulPointTm(eye_to_point, hero_bbox_reprojectionX, hero_bbox_reprojectionY, hero_bbox_reprojectionZ);
    }

    float3 just_apply_hero_matrix(float3 eye_to_point)
    {
      return mulPointTm(eye_to_point, prev_hero_matrixX, prev_hero_matrixY, prev_hero_matrixZ);
    }
    bool isInHero(bool isHeroCockpit, float3 eye_to_point)
    {
      return hero_is_cockpit ? isHeroCockpit : isHeroCockpit || is_inside_hero_bbox(eye_to_point);
    }
  }
endmacro

macro USE_HERO_MATRIX_WITH_GBUF(code)
  USE_HERO_MATRIX_NO_GBUF(code)
  hlsl(code) {
    bool apply_hero_matrix(float2 uv, inout float3 eye_to_point)
    {
##if gbuffer_has_hero_cockpit
      bool ret = isInHero(isGbufferHeroCockpit(uv), eye_to_point);
##else
      bool ret = is_inside_hero_bbox(eye_to_point);
##endif
      eye_to_point = ret ? just_apply_hero_matrix(eye_to_point) : eye_to_point;
      return ret;
    }
  }
endmacro

macro USE_HERO_MATRIX(code)
  USE_READ_IS_HERO_COCKPIT(code)
  USE_HERO_MATRIX_WITH_GBUF(code)
endmacro