include "antialiasing_type.inc.dshl"
include "distortion_postfx_inc.dshl"
include "heatHazeOffset.dshl"
include "postfx_common.dshl"

shader distortion_postfx {
  color_write = rgba;

  INIT_HEAT_HAZE_OFFSET()
  USE_HEAT_HAZE_OFFSET()
  COMMON_POSTFX()

  hlsl(ps) {
    half3 sampleSceneColor(float2 uv)
    {
      return tex2Dlod(frame_tex, float4(uv,0,0)).rgb;
    }
    float sampleSceneAlpha(float2 uv)
    {
      return tex2Dlod(frame_tex, float4(uv,0,0)).a;
    }
  }

  INIT_DISTORTION_POSTFX()
  USE_DISTORTION_POSTFX()

  local float4 frame_tex_resolution = get_dimensions(frame_tex, 0);

  (ps) {
    frame_tex@smp2d = frame_tex;
    frame_tex_resolution@f4 = (frame_tex_resolution.xy, 1/frame_tex_resolution.xy);
  }

  hlsl(ps) {
    float4 distortion_postfx_ps(VsOutput input HW_USE_SCREEN_POS) : SV_Target0
    {
    ##if antialiasing_type == tsr
      float pseudoDepth = sampleSceneAlpha(input.tc);
      float alpha = pseudoDepth;
    ##else
      float alpha = 1.0;
    ##endif

      float3 frame = apply_distortion_postfx(input);
      return float4(frame, alpha);
    }
  }

  compile("target_vs", "postfx_vs");
  compile("target_ps", "distortion_postfx_ps");
}