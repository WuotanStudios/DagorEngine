include "shader_global.dshl"
include "psh_tangent.dshl"
include "normaldetail.dshl"
include "gbuffer.dshl"
include "static_shadow.dshl"
include "giHelpers/voxelize_gs.dshl"

include "world_sdf_rasterize_sdf_common.dshl"

macro SET_WORLD_VIEW_POS()
  (ps) { world_view_pos@f3 = world_view_pos; }
  (vs) { world_view_pos@f3 = world_view_pos; }
endmacro

int scene_mode = 0;
interval scene_mode: normal<1, voxelize_albedo<2, voxelize_sdf;
int voxelize_axis;

shader sponza, simple_aces
{

  if (two_sided)
  {
    (ps) { is_two_sided@i1 = (1,0,0,0); }
  } else
  {
    (ps) { is_two_sided@i1 = (0,0,0,0); }
  }
  if (scene_mode != normal)
  {
    hlsl {
      //#undef SUPPORT_LATE_INTERPOLATION
      #define VOXELIZE_CENTROID centroid
    }
    (vs) {voxelize_axis@i1 = (voxelize_axis);}
  } else
  {
    hlsl {
      #define VOXELIZE_CENTROID
    }
  }
  SET_WORLD_VIEW_POS()
  texture tex = material.texture.diffuse;
  texture normal = material.texture[2];

  if (shader == sponza)
  {
    texture spec = material.texture[4];
    texture mask = material.texture[6];
  }

  if (scene_mode != normal)
  {
    z_write = false;
    z_test = false;
  }
  if (scene_mode == voxelize_sdf)
  {
    if (supports_sh_6_1 == on)
    {
      hlsl {
        #define RASTERIZE_PRIMS 1
        #define SUPPORT_LATE_INTERPOLATION 1
      }
    }
  }

  if (scene_mode == voxelize_albedo)
  {
    DAGI_ALBEDO_WRITE(ps)
  }

  //blend_src = one; blend_dst = zero;
  //blend_asrc = one; blend_adst = one;   // For water reflection.
  WRITE_GBUFFER()

  hlsl {
    struct VsOutput
    {
      VS_OUT_POSITION(pos)
      float2 diffuseTexcoord              : TEXCOORD0;
      float3 normal                       : TEXCOORD5;
      ##if (scene_mode != normal)
      nointerpolation int axis:TEXCOORD1;
      VOXELIZE_CENTROID float3 worldPos            : TEXCOORD2;
      #if RASTERIZE_PRIMS
      nointerpolation float3 vertexWorldPos: TEXCOORD3;
      #endif
      ##else
      VOXELIZE_CENTROID float3 pointToEye          : TEXCOORD2;
      ##endif
    };
  }

  if (scene_mode == voxelize_sdf)
  {
    INIT_RASTERIZE_SDF()
    PROJECT_WORLD_SDF_RASTERIZE(ps)
  }

  if (scene_mode != normal)
  {
    TO_VOXELIZE_SPACE(vs)
    USE_VOXELIZE_SPACE(vs)
  }

  (ps) { diffuse_tex@static = tex; }
  if (normal != NULL)
  {
    USE_PIXEL_TANGENT_SPACE()
    (ps) { normal_tex@static = normal; }
  }
  if (shader == sponza)
  {
    if (spec != NULL)
    {
      (ps) { spec@static = spec; }
    }
    if (mask != NULL)
    {
      (ps) { mask@static = mask; }
    }
  } else
  {
    static int atest = 0;
    interval atest : atestOff < 1, atestOn;
    if (atest == atestOn)
    {
      USE_ATEST_HALF()
    }
    else
    {
      NO_ATEST()
    }
  }

  //fsh_const[3] = (atest / 255.0, 0, 0, 0);

  hlsl(ps) {
    ##if scene_mode == voxelize_sdf
      void main_ps_simple_aces(VsOutput input INPUT_VFACE)
      {
        float2 texCoord = input.diffuseTexcoord;
        half4 diffuseColor = tex2DBindless(get_diffuse_tex(), texCoord);
        ##if (shader == sponza)
          ##if (mask != NULL)
            half alpha = tex2DBindless(get_mask(), texCoord).x;
            clip(alpha-0.5);
          ##endif
        ##else
          ##if (atest == atestOn)
            clip_alpha(diffuseColor.a);
          ##endif
        ##endif
        WorldSDFInterpolant sdfI = (WorldSDFInterpolant)0;
        sdfI.worldPos = input.worldPos;
        sdfI.axis = input.axis;
        #if RASTERIZE_PRIMS
        sdfI.va = GetAttributeAtVertex(input.vertexWorldPos, 0);
        sdfI.vc = GetAttributeAtVertex(input.vertexWorldPos, 1);
        sdfI.vb = GetAttributeAtVertex(input.vertexWorldPos, 2);
        #else
        SET_IF_IS_BACK_FACE(sdfI.isBackFace, true);
        #endif
        sdfI.isTwoSided = get_is_two_sided();//false

        write_world_sdf(sdfI);
        half3 worldNormal = normalize(input.normal);
        project_world_sdf_rasterize_albedo(sdfI, worldNormal, true, diffuseColor.xyz + 0.04h);
      }
    ##elif scene_mode == voxelize_albedo
      void main_ps_simple_aces(VsOutput input)
      {
        float2 texCoord = input.diffuseTexcoord;
        half4 diffuseColor = tex2DBindless(get_diffuse_tex(), texCoord);
        ##if (shader == sponza)
          ##if (mask != NULL)
            half alpha = tex2DBindless(get_mask(), texCoord).x;
            clip(alpha-0.5);
          ##endif
        ##else
          ##if (atest == atestOn)
            clip_alpha(diffuseColor.a);
          ##endif
        ##endif

        float3 worldPos = input.worldPos;
        dagi_write_updated_albedo_legacy(worldPos, saturate(diffuseColor.rgb + 0.04), 0.5);
      }
    ##elif scene_mode == normal
      GBUFFER_OUTPUT main_ps_simple_aces(VsOutput input INPUT_VFACE HW_USE_SCREEN_POS)
      {
        float3 worldPos = world_view_pos - input.pointToEye;
        float2 texCoord = input.diffuseTexcoord;
        half4 diffuseColor = tex2DBindless(get_diffuse_tex(), texCoord);
        ##if (shader == sponza)
          ##if (mask != NULL)
            half alpha = tex2DBindless(get_mask(), texCoord).x;
            clip(alpha-0.5);
          ##endif
        ##else
          ##if (atest == atestOn)
            clip_alpha(diffuseColor.a);
          ##endif
        ##endif

        half3 vertexNormal = normalize(input.normal);

        half smoothness = 0;
        ##if shader == sponza
          ##if spec != NULL
            half specularMask = tex2DBindless(get_spec(), texCoord).x;
          ##else
            half specularMask = 0;
          ##endif
          smoothness = 0.5*pow8(specularMask);
        ##endif
        ##if normal != NULL
          half4 packedNormalMap = tex2DBindless(get_normal_tex(), input.diffuseTexcoord);
          ##if shader == sponza
            half3 normalMap = restore_normal(packedNormalMap.xy);
            half metallTranslucency = 0;
          ##else
            half3 normalMap = unpack_ag_normal(packedNormalMap);
            half specularMask = packedNormalMap.r;
            smoothness = pow2(specularMask);
            half metallTranslucency = packedNormalMap.b;
          ##endif
          half3 worldNormal = perturb_normal( normalMap, vertexNormal, worldPos, input.diffuseTexcoord );
        ##else
          half3 worldNormal = vertexNormal;
        ##endif

        UnpackedGbuffer gbuffer;
        init_gbuffer(gbuffer);
        if (get_is_two_sided())
          worldNormal = MUL_VFACE(worldNormal);
        //float3 worldPos = world_view_pos-input.pointToEye;
        //diffuseColor.rgb = frac(worldPos.xzz*0.1+0.01);
        init_albedo(gbuffer, diffuseColor.rgb);
        init_smoothness(gbuffer, smoothness);
        init_normal(gbuffer, worldNormal);
        half ao = 1, shadow = 1;
        ##if (!two_sided)
        {
          SET_IF_IS_BACK_FACE(ao, ao*0.125);
          SET_IF_IS_BACK_FACE(shadow, 0);
        }
        ##endif
        init_ao(gbuffer, ao);
        init_shadow(gbuffer, shadow);

        return encode_gbuffer(gbuffer, input.pointToEye, input.pointToEye);
      }
    ##endif
  }
  if (scene_mode == voxelize_sdf && supports_sh_6_1 == on)
  {
    compile("ps_6_1", "main_ps_simple_aces");
  } else
  {
    compile("target_ps", "main_ps_simple_aces");
  }


  channel float3 pos=pos;
  channel color8 norm=norm unsigned_pack;
  channel float2 tc[0]=tc[0];


  (vs) { globtm@f44 = globtm; }

  if (in_editor == yes)
  {
    (vs) {
      world_local_x@f3 = world_local_x;
      world_local_y@f3 = world_local_y;
      world_local_z@f3 = world_local_z;
      world_local_pos@f3 = world_local_pos;
    }
  }


  hlsl(vs) {
    struct VsInput
    {
      float3 pos: POSITION;  // W defaults to 1.
      float3 packedNormal             : NORMAL;
      float2 diffuseTexcoord          : TEXCOORD0;
    };

    VsOutput main_vs_all_simples_aces(VsInput input)
    {
      VsOutput output;

##if in_editor == yes
      float3 worldPos = input.pos.x * world_local_x + input.pos.y * world_local_y + input.pos.z * world_local_z + world_local_pos;
##else
      float3 worldPos = input.pos;
##endif

##if in_editor == yes
      float3 normal = input.packedNormal * 2. - 1.;
      float3 worldNormal = normalize(normal.x * world_local_x + normal.y * world_local_y + normal.z * world_local_z);
##else
      float3 worldNormal = BGR_SWIZZLE(input.packedNormal) * 2. - 1.;
##endif
      ##if scene_mode == normal
      output.pos = mul(float4(input.pos,1), globtm);
      output.pointToEye = world_view_pos.xyz - worldPos.xyz;
      ##else
      //float3 boxPos = (((worldPos-voxelize_box0)*voxelize_box1)*2-1)*voxelize_aspect_ratio;
      output.pos = worldPosToVoxelSpace(worldPos, voxelize_axis);

      #if RASTERIZE_PRIMS
      output.vertexWorldPos = worldPos;
      #endif
      output.axis = voxelize_axis;
      output.worldPos = worldPos.xyz;
      ##endif

      output.diffuseTexcoord.xy = input.diffuseTexcoord;

      output.normal = worldNormal;
      return output;
    }
  }

  compile("target_vs", "main_vs_all_simples_aces");
}

shader gi_black
{

  cull_mode = none;
  SET_WORLD_VIEW_POS()
  if (scene_mode == normal)
  {
    dont_render;
  }

  if (scene_mode != normal)
  {
    z_write = false;
    z_test = false;
  }

  if (scene_mode == voxelize_sdf)
  {
    hlsl(ps) {
      RWTexture3D<float> world_sdf_clipmap : register(u0);
    }
    (vs) {voxelize_axis@i1 = (voxelize_axis);}
  } else if (scene_mode == voxelize_albedo)
  {
    DAGI_ALBEDO_WRITE(ps)
  }


  //blend_src = one; blend_dst = zero;
  //blend_asrc = one; blend_adst = one;   // For water reflection.
  WRITE_GBUFFER()

  hlsl {
    struct VsOutput
    {
      VS_OUT_POSITION(pos)
      float3 worldPos                   : TEXCOORD2;
      ##if scene_mode == voxelize_sdf
      nointerpolation int axis:TEXCOORD1;
      ##endif
    };
  }
  if (scene_mode == voxelize_sdf)
  {
    INIT_RASTERIZE_SDF()
  }

  hlsl(ps) {
    void main_ps_simple_aces(VsOutput input)
    {
      ##if scene_mode == voxelize_sdf
        WorldSDFInterpolant sdfI;
        sdfI.worldPos = input.worldPos;
        sdfI.axis = input.axis;
      write_world_sdf(sdfI);
      ##elif scene_mode == voxelize_albedo
      dagi_write_updated_albedo(input.worldPos, 0);
      ##endif
      //half3 litColor = diffuseColor.rgb*lightColor*getStaticShadow(float4(worldPos,1));
    }
  }
  compile("target_ps", "main_ps_simple_aces");


  channel float3 pos=pos;

  (vs) { globtm@f44 = globtm; }

  if (in_editor == yes)
  {
    (vs) {
      world_local_x@f3 = world_local_x;
      world_local_y@f3 = world_local_y;
      world_local_z@f3 = world_local_z;
      world_local_pos@f3 = world_local_pos;
    }
  }


  hlsl(vs) {
    struct VsInput
    {
      float4 pos: POSITION;  // W defaults to 1.
    };

    VsOutput main_vs_all_simples_aces(VsInput input)
    {
      VsOutput output;

##if in_editor == yes
      float4 worldPos = float4(input.pos.x * world_local_x + input.pos.y * world_local_y + input.pos.z * world_local_z + world_local_pos, 1);
##else
      float4 worldPos = input.pos;
##endif

      output.pos = mul(input.pos, globtm);

      output.worldPos = worldPos.xyz;
      ##if scene_mode == voxelize_sdf
      output.axis = voxelize_axis;
      ##endif

      return output;
    }
  }

  compile("target_vs", "main_vs_all_simples_aces");
}
