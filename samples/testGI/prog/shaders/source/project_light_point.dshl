include "static_shadow.dshl"
include "csm.dshl"
include "screenspace_probes_common.dshl"
include "dagi_irrad_grid_sample.dshl"
include "dagi_common_ambient.dshl"
include "clustered/gi_dynamic_lights.dshl"

macro SUN_LIGHT_POINT_TRACE_HIT(code)//at trace hit when calculating probes
  INIT_STATIC_SHADOW_BASE(code)
  hlsl(code) {
    #define HAS_STATIC_SHADOW 1
    #define FASTEST_STATIC_SHADOW_PCF 1
  }
  USE_STATIC_SHADOW_BASE(code)
  (code) {
    to_sun_direction@f3 = (-from_sun_direction.x, -from_sun_direction.y, -from_sun_direction.z, 0.0)
    base_sun_color_0@f3 = (sun_color_0.r, sun_color_0.g, sun_color_0.b, 0);
    current_world_view_pos@f3 = world_view_pos;
    csm_world_view_pos@f3 = csm_world_view_pos;
  }
  hlsl(code) {
    float3 dagi_get_sun_color(float3 worldPos, float3 worldNormal, float shadow_offset = 0)
    {
      float NoL = saturate(dot(worldNormal, to_sun_direction));
      if (NoL == 0)
        return 0;
      float3 offsetedWorldPos = worldPos + shadow_offset*to_sun_direction;
      #ifdef NUM_CASCADES
        //using CSM is only 5% slower than just use static cascade, but obviously more accurate
        //todo: disable on prev-gen / bare minimum
        float3 csmShadow = get_csm_shadow( csm_world_view_pos.xyz - offsetedWorldPos, 0 );
        float shadow = csmShadow.z ? getStaticShadow(offsetedWorldPos) : csmShadow.x;
      #else
        float shadow = getStaticShadow(offsetedWorldPos);
      #endif
      return (NoL*shadow)*base_sun_color_0;
    }
  }
endmacro

macro PROJECT_ADDITIONAL_SKY_VIS(code)//at trace hit when calculating sky vis probes
  //INIT_CSM_SHADOW(code)
  //BASE_USE_CSM_SHADOW_DEF_NUM(code)
  SUN_LIGHT_POINT_TRACE_HIT(code)
  DAGI_SKY_VIS_THROUGH(code)
endmacro

macro SUN_SKY_LIGHT_POINT_TRACE_HIT(code)//at trace hit when calculating probes (currently also used for initial voxelization. However, direct lights are different for that)
  SUN_LIGHT_POINT_TRACE_HIT(code)
  DAGI_GET_COMMON_VOXELIZATION_AMBIENT(code)
  hlsl(code) {
    float3 get_sun_sky_lit_color(float3 ambientWorldPos, float3 worldPos, float3 worldNormal, float3 albedo, float shadow_offset = 0, float ambient_loss = 1)
    {
      float3 ambient = dagi_get_common_offscreen_ambient(ambientWorldPos, worldNormal);
      float3 litColor = (ambient_loss*ambient + dagi_get_sun_color(worldPos, worldNormal, shadow_offset))*albedo;
      //fixme: add direct lights from camera
      return litColor;
    }
    #define DAGI_LIGHT_TRANSLUCENT_SCENE 1
    float3 get_sun_sky_lit_color_both(float3 ambientWorldPos, float3 worldPos, float3 worldNormal, float3 albedo, float3 translucencyColor, inout float3 ambient, inout float3 back_ambient, float shadow_offset = 0, float ambient_loss = 1)
    {
      dagi_get_common_offscreen_ambient_both(ambientWorldPos, worldNormal, ambient, back_ambient);
      float3 litColor = (ambient_loss*ambient + dagi_get_sun_color(worldPos, worldNormal, shadow_offset))*albedo +
                        (ambient_loss*back_ambient + dagi_get_sun_color(worldPos, -worldNormal, shadow_offset))*translucencyColor;
      //fixme: add direct lights from camera
      return litColor;
    }
  }
endmacro

texture combined_shadows;

macro ON_SCREEN_SIMPLE_SUN_LIGHTING(code)
  (code) {
    combined_shadows@smp2d = combined_shadows;
  }
  hlsl(code) {
    float dagi_get_on_screen_simple_sun_shadow(float2 uv)
    {
      return tex2Dlod(combined_shadows, float4(uv,0,0)).x;
    }
    float3 dagi_get_on_screen_simple_sun_light(float3 worldPos, float2 uv, float3 worldNormal)
    {
      float NoL = saturate(dot(worldNormal, to_sun_direction));
      if (NoL == 0)
        return 0;
      return (NoL*dagi_get_on_screen_simple_sun_shadow(uv))*base_sun_color_0;
    }
    float3 dagi_get_on_screen_simple_sun_light_with_shadow(float3 worldNormal, float shadow)
    {
      return saturate(dot(worldNormal, to_sun_direction)*shadow)*base_sun_color_0;
    }
  }
endmacro

macro PROJECT_LIGHT_POINT_SCREEN_VOXEL(code) // when updating radiance from gbuf
  DAGI_IRRADIANCE_GRID_SAMPLE(code)
  SAMPLE_IRRADIANCE_SP(code)
  PACK_UNPACK_GBUFFER_BASE(code)
  (code) {
    to_sun_direction@f3 = (-from_sun_direction.x, -from_sun_direction.y, -from_sun_direction.z, 0.0)
    base_sun_color_0@f3 = (sun_color_0.r, sun_color_0.g, sun_color_0.b, 0);
    dagi_irradiance_world_view_pos@f3 = world_view_pos;
  }
  ON_SCREEN_SIMPLE_SUN_LIGHTING(code)
  hlsl(code) {
    float3 project_get_lit_color_at_screen_translucency(float2 tc, float2 screenpos, float linearDepth, float3 worldPos, ProcessedGbuffer gbuf,float depthW = -1000)
    {
      float3 ambientWorldPos = worldPos;//fixme!
      float shadowBiasMeters = 0;
      float3 ambient = 0, backAmbient = 0;
      //we use screenprobes irradiance
      float3 toCamera = dagi_irradiance_world_view_pos - worldPos;
      BRANCH
      if (!sp_getNumScreenProbes() || !sample_bilinear_irradiance_envi_normal_both_campos(gbuf.normal, linearDepth, -toCamera, gbuf.normal, screenpos.xy, ambient, backAmbient, depthW))
      {
        //should not be happening normally...
        float len = length(toCamera);
        dagi_irrad_grid_diffuse_biased_both_dir(ambientWorldPos, gbuf.normal, toCamera/max(1e-9, len), len, gbuf.normal, ambient, backAmbient);
      }
      //add back light from foliage
      //todo:
      // * add SSAO for ambient?
      float sunShadow = dagi_get_on_screen_simple_sun_shadow(tc);
      half3 albedo = gbuf.diffuseColor + gbuf.specularColor;
      float3 litColor = (ambient*gbuf.ao + dagi_get_on_screen_simple_sun_light_with_shadow(gbuf.normal, sunShadow))*albedo +
                        (backAmbient*gbuf.ao + dagi_get_on_screen_simple_sun_light_with_shadow(-gbuf.normal, sunShadow))*gbuf.translucencyColor;
      //fixme: add direct lights from camera
      return litColor;
    }
    float3 project_get_lit_color_at_screen(float2 tc, float2 screenpos, float linearDepth, float3 worldPos, ProcessedGbuffer gbuf, float depthW = -1000)
    {
      float3 ambientWorldPos = worldPos;//fixme!
      float shadowBiasMeters = 0;
      float3 ambient = 0;
      //we use screenprobes irradiance
      float3 toCamera = dagi_irradiance_world_view_pos-worldPos;
      BRANCH
      if (!sp_getNumScreenProbes() || !sample_bilinear_irradiance_campos(linearDepth, -toCamera, gbuf.normal, screenpos.xy, ambient, depthW))
      {
        //should not be happening normally...
        float len = length(toCamera);
        dagi_irrad_grid_diffuse_biased(ambientWorldPos, gbuf.normal, toCamera/max(1e-9, len), len, gbuf.normal, ambient);
      }
      //todo:
      // * add SSAO for ambient?
      half3 albedo = gbuf.diffuseColor + gbuf.specularColor;
      float3 litColor = (ambient*gbuf.ao + dagi_get_on_screen_simple_sun_light(worldPos, tc, gbuf.normal))*albedo;
      #if GI_HAS_DYNAMIC_LIGHTS
        litColor += getGiDynamicLighting(gbuf, worldPos, screenpos, tc, linearDepth);
      #endif
      return litColor;
    }
  }
endmacro

macro PROJECT_LIGHT_POINT_TRACE_HIT(code)//at trace hit when calculating probes
  SUN_SKY_LIGHT_POINT_TRACE_HIT(code)
  //INIT_CSM_SHADOW(code)
  //BASE_USE_CSM_SHADOW_DEF_NUM(code)
  hlsl(code) {
    float3 project_get_lit_color_at_trace_hit(float3 worldPos, float3 worldNormal, float3 albedo)
    {
      float3 ambientWorldPos = worldPos;//fixme!
      float shadowBiasMeters = 0;//fixme: we should add bias based on distance
      return get_sun_sky_lit_color(ambientWorldPos, worldPos, worldNormal, albedo, shadowBiasMeters);
    }
  }
endmacro

macro PROJECT_LIGHT_POINT_VOXELIZE(code) // when voxelizing radiance of radiance voxel scene
  INIT_CSM_SHADOW(code)
  BASE_USE_CSM_SHADOW_DEF_NUM(code)
  SUN_SKY_LIGHT_POINT_TRACE_HIT(code)
  hlsl(code) {
    float3 project_get_lit_color_at_voxelize(float3 worldPos, float3 worldNormal, float3 albedo)
    {
      float halfVoxelSize = 0.5*world_sdf_raster_voxel_size();
      float3 ambientWorldPos = current_world_view_pos.xyz + (worldPos - current_world_view_pos.xyz)*0.99;
      #ifdef WORLD_SDF_STEP_OUT
      uint stepOutMip = world_sdf_update_mip+1;
      ambientWorldPos += worldNormal*halfVoxelSize;
      if (stepOutMip < world_sdf_num_cascades())
        world_sdf_step_out_clip_unsafe(ambientWorldPos, stepOutMip, 0, 1);
      #endif
      float3 litColor = get_sun_sky_lit_color(ambientWorldPos,
        worldPos, worldNormal, albedo, halfVoxelSize, 0.5);
      #if GI_HAS_DYNAMIC_LIGHTS
        //UnpackedGbuffer gbufU;
        //init_gbuffer(gbufU);
        ProcessedGbuffer gbuf;
        gbuf.albedo = gbuf.diffuseColor = albedo;
        gbuf.normal = worldNormal;
        litColor += getGiDynamicLighting(gbuf, worldPos, 0, 0, 0);
      #endif
      return litColor;
    }
  }
endmacro

macro PROJECT_LIGHT_POINT_OFF_SCREEN_DYNAMIC_LIGHTS(code)
  GI_DYNLIGHTS_MIN_PROCESSED_GBUFFER(code)
  GI_OUT_OF_SCREEN_VOXELIZE_LIGHT_HELPER(code)
endmacro

macro PROJECT_LIGHT_POINT_VOXELIZE_OFF_SCREEN_VOXEL_LIT_SCENE(code)
  PROJECT_LIGHT_POINT_OFF_SCREEN_DYNAMIC_LIGHTS(code)
  PROJECT_LIGHT_POINT_VOXELIZE(code)
endmacro


macro PROJECT_LIGHT_POINT_SCREEN_DYNAMIC_LIGHTS(code)
  GI_DYNLIGHTS_MIN_PROCESSED_GBUFFER(code)
  GI_COMMON_SCREEN_DYNAMIC_LIGHTS(code)
endmacro

macro PROJECT_LIGHT_POINT_SCREEN_VOXEL_LIT_SCENE(code)
  PACK_UNPACK_GBUFFER_BASE(code)
  PROJECT_LIGHT_POINT_SCREEN_DYNAMIC_LIGHTS(code)
  PROJECT_LIGHT_POINT_SCREEN_VOXEL(code)
endmacro

